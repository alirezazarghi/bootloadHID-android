apply plugin: 'com.android.application'

def PACKAGE_NAME = "com.dena.app.bootloadhid"
android {
    compileSdkVersion 23
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file(debugKeystore)
        }
        release {
            storeFile file(productKeystore)
            keyAlias productKeyAlias
            storePassword productKeyPass
            keyPassword productAliasPassword
        }
    }
    buildTypes {
        debug{
            debuggable true
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    android.applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {
            def date = new java.text.SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
            def gitHash = getGitHash();
            variant.outputs.each { output ->
                def newName = "${getSimplePackageName(PACKAGE_NAME)}-${date}-${gitHash}.apk"
                output.outputFile = new File(output.outputFile.parent, newName)
            }
        }
    }
}

def getGitHash() {
    'git rev-parse --short HEAD'.execute(null, rootDir).in.text.trim()
}

def getSimplePackageName(name) {
    name.substring name.lastIndexOf(".") + 1
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:preference-v7:23.4.0'
}
